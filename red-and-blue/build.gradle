plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.neboskreb'
version = '1.4.1-SNAPSHOT'

publishing {
    repositories {
        maven {
            name = "GitHub"
            url = uri("https://maven.pkg.github.com/neboskreb/red-and-blue")
            credentials {
                username = project.findProperty("github_packages_user") ?: System.getenv("PACKAGES_USERNAME")
                password = project.findProperty("github_packages_token") ?: System.getenv("PACKAGES_TOKEN")
            }
        }
        maven {
            name = "MavenCentral"
            if (version.endsWith('SNAPSHOT')) {
                url = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
            } else  {
                url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
            }
            credentials {
                username = project.findProperty("mavenCentralUser") ?: System.getenv("MAVEN_USERNAME")
                password = project.findProperty("mavenCentralPass") ?: System.getenv("MAVEN_PASSWORD")
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            artifactId = 'red-and-blue'

            from components.java

            pom {
                name = 'Red and Blue extension'
                description = 'JUnit 5 extension for easy injection of Red and Blue instances'
                url = 'https://github.com/neboskreb/red-and-blue'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'JBM'
                        name = 'John Y. Pazekha'
                        email = 'neboskreb@narod.ru'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:neboskreb/red-and-blue.git'
                    developerConnection = 'scm:git:ssh://github.com:neboskreb/red-and-blue.git'
                    url = 'https://github.com/neboskreb/red-and-blue'
                }
            }
        }

    }
}



signing {
    sign publishing.publications.mavenJava
}

java {
    withSourcesJar()
    withJavadocJar()
    sourceCompatibility = 11
    targetCompatibility = 11
}

test {
    useJUnitPlatform()
}

configurations {
    configureEach {
        resolutionStrategy {
            force 'nl.jqno.equalsverifier:equalsverifier:4.2.1'
        }
    }
}

dependencies {
    implementation 'nl.jqno.equalsverifier:equalsverifier:[4.0, 5)'
    implementation 'org.apache.commons:commons-lang3:3.19.0'

    compileOnly platform('org.junit:junit-bom:[5.0.0,7)')
    compileOnly 'org.junit.jupiter:junit-jupiter'
    compileOnly 'org.junit.jupiter:junit-jupiter-engine'


    testImplementation platform('org.junit:junit-bom:6.0.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly    'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.mockito:mockito-core:5.20.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.20.0'
}
